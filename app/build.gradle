apply plugin: 'com.android.application'
apply plugin: 'android-rocket-launcher'

// get signing properties
Properties keystoreProperties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    if (localProperties.getProperty('signingPropertiesFile') != null
            && new File(localProperties.getProperty('signingPropertiesFile')).exists()) {
        keystoreProperties.load(
                new FileInputStream(file(localProperties.getProperty('signingPropertiesFile'))))
    }
}
if (keystoreProperties.isEmpty()) {
    keystoreProperties.setProperty('storeFile', 'empty.keystore')
    keystoreProperties.setProperty('storePassword', 'storePassword')
    keystoreProperties.setProperty('keyAlias', 'keyAlias')
    keystoreProperties.setProperty('keyPassword', 'keyPassword')
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "org.walkersguide.android"
        resConfigs "en", "de"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 9
        versionName '0.4.0'
        // personal data
        buildConfigField 'String', 'CONTACT_EMAIL', '"support@walkersguide.org"'
        buildConfigField 'String', 'CONTACT_WEBSITE', '"http://walkersguide.org"'
        // internal
        // database
        buildConfigField 'String', 'DATABASE_NAME', '"walkersguide.db"'
        buildConfigField 'Integer', 'DATABASE_VERSION', '9'
        // server
        buildConfigField 'String', 'SERVER_URL', '"https://scheibler-dresden.homelinux.org/walkersguide/prod/"'
        buildConfigField 'Integer', 'SUPPORTED_API_VERSION', '1'
        buildConfigField 'boolean', 'DEFAULT_SHOW_DEVELOPMENT_MAPS', 'false'
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField 'boolean', 'DEFAULT_SHOW_DEVELOPMENT_MAPS', 'true'
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }

    lintOptions {
        disable "StaticFieldLeak", "InflateParams", "GoogleAppIndexingWarning",
                "ButtonStyle", "UseSparseArrays", "UseCompoundDrawables", "DefaultLocale"
    }
}

dependencies {
    compile 'com.android.support:design:27.0.2'
    // guava: google java library
    compile 'com.google.guava:guava:20.0'
    // simple gesture library dependency
	compile 'in.championswimmer:SimpleFingerGestures_Android_Library:1.2'
}

// REQUIRED: Google's new Maven repo is required for the latest
// support library that is compatible with Android 8.0
repositories {
    // gradle version >= 4.1
    // google()
    //
    // If you're using a version of Gradle lower than 4.1, you must instead use:
    maven {
        url 'https://maven.google.com'
    }
    // simple gesture library repository
    maven {
        url 'https://jitpack.io'
    }
}
